import com.github.gradle.node.npm.task.NpmTask
import org.apache.tools.ant.filters.ReplaceTokens
import java.nio.charset.StandardCharsets

plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '4.0.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.github.node-gradle.node" version "7.1.0"
}

group = 'live.omnisource'
version = (gitTagVersion() ?: findProperty("appVersion") ?: "0.0.0-dev")
description = 'tessera'

ext {
    h3Version = findProperty("h3Version") ?: "4.4.0"
    jtsVersion = findProperty("jtsVersion") ?: "1.20.0"
    cassandraDriverVersion = '4.19.2'
    elasticsearchVersion = '8.16.0'
    flywayVersion        = '11.1.0'
    oraDependencyVersion = '23.6.0.24.10'
    postgisJdbcVersion   = '2023.1.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // Spring Starters
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-batch-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-authorization-server'
    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Spring Extensions
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    // Internal DB management / Postgres
    implementation 'org.postgresql:postgresql'
    implementation 'com.zaxxer:HikariCP:6.2.1'
    implementation 'org.flywaydb:flyway-database-postgresql'

    // Serialization
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Geospatial Tooling
    implementation "com.uber:h3:${h3Version}"
    implementation "org.locationtech.jts:jts-core:${jtsVersion}"
    implementation "org.locationtech.jts.io:jts-io-common:${jtsVersion}"
    implementation 'org.hibernate.orm:hibernate-spatial'

    // Cassandra
    implementation "org.apache.cassandra:java-driver-core:${cassandraDriverVersion}"
    implementation "org.apache.cassandra:java-driver-query-builder:${cassandraDriverVersion}"
    implementation "org.apache.cassandra:java-driver-mapper-runtime:${cassandraDriverVersion}"


    // Annotation Processors
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-batch-jdbc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-cassandra-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jdbc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-elasticsearch-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-thymeleaf-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir("reports/jacocoHtml")
    }
}

jacoco {
    toolVersion = "0.8.14"
}

def gitTagVersion() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "describe", "--tags", "--abbrev=0"
            standardOutput = stdout
            errorOutput = new ByteArrayOutputStream()
            ignoreExitValue = true
        }
        def tag = stdout.toString().trim()
        if (!tag) return null
        return tag.replaceFirst(/^v/, "")
    } catch (Exception ignored) {
        return null
    }
}

def frontendDir = file("src/main/frontend")

node {
    download = true
    version = "20.11.1"        // pick a Node LTS version you standardize on
    npmVersion = "10.2.4"      // optional; you can omit to use bundled npm
    workDir = layout.buildDirectory.dir("node")
    npmWorkDir = layout.buildDirectory.dir("npm")
    nodeProjectDir = frontendDir
}

tasks.register("npmCi", NpmTask) {
    group = "frontend"
    description = "npm ci in src/main/frontend"
    args = ["ci"]
}

tasks.register("npmBuild", NpmTask) {
    group = "frontend"
    description = "npm run build in src/main/frontend"
    dependsOn "npmCi"
    args = ["run", "build"]

    inputs.files(
            file("${frontendDir}/package.json"),
            file("${frontendDir}/package-lock.json"),
            fileTree(frontendDir).matching {
                include "tailwind.config.*"
                include "postcss.config.*"
                include "main.css"
            },
            fileTree("src/main/resources/templates").matching { include "**/*.html" }
    )
    outputs.file(file("src/main/resources/static/main.css"))
}

tasks.named("processResources") {
    dependsOn "npmBuild"
}

tasks.named("processResources") {
    filteringCharset = StandardCharsets.UTF_8.name()
    filesMatching(["**/application.yml", "**/application.yaml"]) {
        filter(ReplaceTokens, tokens: [
                appVersion: project.version.toString(),
                h3Version : project.ext.h3Version.toString(),
                jtsVersion : project.ext.jtsVersion.toString()
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += '-parameters'
}
